data auto;
infile '/home/jlingle0/sasuser.v94/6372/Data Files/Automobile_data.csv' dlm=',' firstobs=2;

input symboling normalizedlosses make $ fueltype $ aspiration $ numofdoors $ bodystyle $ drivewheels $ enginelocation $ wheelbase length width height curbweight enginetype $ numofcylinders $ enginesize fuelsystem $ bore stroke compressionratio horsepower peakrpm citympg highwaympg price;
run;


*Calculating a summary stats table and outputing the results in a dataset called "meansout";

data auto2;
set auto;
if numofdoors = '?' then delete;
if Make in ('honda','isuzu','mazda','mitsubishi','nissan','subaru','toyota','mitsubis','mazda') then region_manufactured = 'Asia';
if Make in ('alfa-romero','audi','bmw','jaguar','mercedes-benz','peugot','porsche','renault','saab','volkswagen','volvo','volkswag','mercedes','alfa-rom') then region_manufactured = 'Europe';
if Make in ('chevrolet','dodge','mercury','chevrole','plymouth') then region_manufactured = 'US';

run;

proc means data=auto2 n mean max min range std fw=8;
class region_manufactured numofdoors;
var Price;
output out=meansout mean=mean std=std;
title 'Summary of Prices';
run;


*The following chunk of code is some basic code to plot the summary statistics in a convenient profile type plot.;
*This will probably take you some time to understand how sas works to finally get the plot but for those who put in the effort, your understanding of SAS
will be better for it and you will soon figure out you can do a lot of differnt things. For those of you who do not have the time, the alternative is to take the summary statistics
output and move them over to excel and create a plot over there.;

data summarystats;
set meansout;
if _TYPE_=0 then delete;
if _TYPE_=1 then delete;
if _TYPE_=2 then delete;
run;


*This data step creates the necessary data set to plot the mean estimates along with the error bars;
data plottingdata(keep=region_manufactured numofdoors mean std newvar);                                                                                                      
   set summarystats;
by region_manufactured numofdoors;
 
   newvar=mean;  
   output;                                                                                                                              
                                                                                                                                        
   newvar=mean - std;                                                                                                                  
   output;                                                                                                                              
                                                                                                                                        
   newvar=mean + std;                                                                                                                  
   output;                                                                                                                              
run;  



*Plotting options to make graph look somewhat decent;
 title1 'Plot Means with Standard Error Bars from Calculated Data for Groups';  

   symbol1 interpol=hiloctj color=vibg line=1;                                                                                          
   symbol2 interpol=hiloctj color=depk line=1;                                                                                          
                                                                                                                                        
   symbol3 interpol=none color=vibg value=dot height=1.5;                                                                               
   symbol4 interpol=none color=depk value=dot height=1.5;  

   axis1 offset=(2,2) ;                                                                                                       
   axis2 label=("Car Prices")  minor=(n=1); 

   *data has to be sorted on the variable which you are going to put on the x axis;
   proc sort data=plottingdata;
   by region_manufactured;
   run;

proc gplot data=plottingdata;
plot NewVar*region_manufactured=numofdoors / vaxis=axis2 haxis=axis1;
*Since the first plot is actually 2 (male female) the corresponding symbol1 and symbol2 options are used which is telling sas to make error bars.  The option is hiloctj;
plot2 Mean*region_manufactured=numofdoors / vaxis=axis2 noaxis nolegend;
*This plot uses the final 2 symbols options to plot the mean points;
run;quit;
*This is the end of the plotting code;


*Running 2way anova analysis;
proc glm data = auto2 plot=meanplot(cl);
  class region_manufactured numofdoors;
  model price = region_manufactured numofdoors region_manufactured * numofdoors;
  lsmeans region_manufactured numofdoors region_manufactured * numofdoors / pdiff=all adjust=tukey;
run;


proc glm data=auto2 PLOTS=all;
class region_manufactured numofdoors;
model price = region_manufactured numofdoors region_manufactured * numofdoors;
lsmeans region_manufactured numofdoors region_manufactured * numofdoors / pdiff tdiff adjust=bon;
estimate 'Asia vs. Europe' region_manufactured -1 1 0 /e;
estimate 'Asia vs. US' region_manufactured -1 0 1 /e;
estimate 'Europe vs. US' region_manufactured 0 -1 1 /e;
estimate 'Four vs Two' numofdoors -1 1 /e;
run;